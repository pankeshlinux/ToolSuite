regions :
 	HousingCommunity : String;
 	House : String;	
	
structs:
	TempStruct
		tempValue : double;
		unitOfMeasurement: String;
	
	SmokePresenceStruct
		smokePresence : boolean;
		timeStamp : long;  

abilities:
	sensors:
		TemperatureSensor
			generate tempMeasurement : TempStruct ;
		
		SmokeDetector
			generate smokePresence : SmokePresenceStruct;
		
	actuators:
		Door
			action Unlock();
			
		Alarm
			action Activate();
			action DeActivate(); 
				
		SprinklerSystem
			action Start();
			action Stop();
	
		Light
			action SwitchOn();
			action SwitchOff();
            
softwarecomponents:
	 computationalService
		 HouseAvgTempComputation
		 	generate houseAvgTemp : TempStruct ;
		 	consume tempMeasurement from region-hops :0 : House ;
		 	partition-per : House;
		 
		 HouseFireComputation
		 	generate houseFireState : SmokePresenceStruct ;
		 	consume  houseAvgTemp from region-hops: 0 :House;
		 	consume  smokePresence from region-hops : 0 : House;
		 	partition-per : House;	
			 	
	controller
	 	HouseFireController
	 	  consume houseFireState from region-hops:0:House;
	 	  command Activate() to region-hops:0:House;
	 	  command DeActivate() to region-hops:0:House;
	 	  command Start() to region-hops:0:House;
	 	  command Stop() to region-hops:0:House;
	 	  command Unlock() to region-hops:0:House;
	 	  partition-per:House;
	 
	 HcFireController
	 		consume houseFireState from region-hops:0:HousingCommunity;
	 		command SwitchOn() to region-hops:0:HousingCommunity;
	 		command SwitchOff() to region-hops:0:HousingCommunity;
	 		partition-per:HousingCommunity;  